# Maintainer: Vlad Zagorodniy <vladzzag@gmail.com>
pkgbase=ttf-meslo
_pkgbase=Meslo-Font
pkgname=(ttf-meslo ttf-meslo-dz)
pkgver=1.2.1
pkgrel=1
arch=('any')
url="https://github.com/andreberg/Meslo-Font"
license=('Apache')
depends=(fontconfig)
makedepends=(unzip)
source=("$_pkgbase-$pkgver.tar.gz"::"https://github.com/andreberg/Meslo-Font/archive/v$pkgver.tar.gz")
sha256sums=('0dbbb60e2ecf59d1cb5ce30e1260e5658c960e0b8d6856c17c1966217e812343')

prepare() {
    cd "$_pkgbase-$pkgver/dist/v$pkgver"

    msg2 "Unpacking Meslo LG"
    unzip "Meslo LG v$pkgver.zip" -d "$srcdir/"

    msg2 "Unpacking Meslo LG DZ"
    unzip "Meslo LG DZ v$pkgver.zip" -d "$srcdir/"
}

package_ttf-meslo() {
    pkgdesc="Customized version of Apple's Menlo font"

    cd "$srcdir/Meslo LG v$pkgver"
    install -Dm644 *.ttf -t "$pkgdir/usr/share/fonts/TTF/"
}

package_ttf-meslo-dz() {
    pkgdesc="Customized version of Apple's Menlo font (dotted zero)"

    cd "$srcdir/Meslo LG DZ v$pkgver"
    install -Dm644 *.ttf -t "$pkgdir/usr/share/fonts/TTF/"
}
