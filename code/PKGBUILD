# Maintainer: Vlad Zagorodniy <vladzzag@gmail.com>
pkgname=code
pkgver=1.27.1
pkgrel=1
pkgdesc="Code Editing. Redefined."
arch=('x86_64')
url="https://code.visualstudio.com/"
license=('custom: commercial')
depends=(alsa-lib cairo fontconfig libxtst gcc-libs gconf gtk2 gvfs libnotify libxss nss python)
source=("$pkgname-$pkgver.tar.gz"::"https://vscode-update.azurewebsites.net/$pkgver/linux-x64/stable"
        code.desktop)
sha256sums=('733b6041431620f2e1caee639a2df3cc6eee418a2c370440909d243055c7f623'
            'e1ea660686fdd3add7dd2d5d707c860fb3324d8d4abcf5a5a4fbaf06abe41da8')

package() {
    install -Dm644 "$srcdir/VSCode-linux-x64/resources/app/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm644 "$srcdir/VSCode-linux-x64/resources/app/resources/linux/code.png" "$pkgdir/usr/share/icons/$pkgname.png"
    install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"

    install -d "$pkgdir/usr/lib"
    cp -pr "$srcdir/VSCode-linux-x64" "$pkgdir/usr/lib/$pkgname"

    install -d "$pkgdir/usr/bin"
    ln -s "/usr/lib/$pkgname/bin/code" "$pkgdir/usr/bin/code"
}
